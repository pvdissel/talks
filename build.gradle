buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.5.0'
        classpath 'org.ajoberstar:gradle-git:1.1.0'
    }
}

import org.asciidoctor.gradle.AsciidoctorTask

// TODO: Somehow create index.html file based on subprojects, linking to each presentation
apply plugin: "org.ajoberstar.github-pages"
githubPages {
    repoUri = 'git@github.com:pvdissel/talks.git'
    pages {
        subprojects.each { p ->
            // TODO: Use output path from 'presentation' task, instead of hardcoding
            from("${p.buildDir}/asciidoc/html5/") {
                rename('presentation.html', 'index.html')
                into p.name
            }
        }

        def tmpDir = file("${buildDir}/tmp")
        tmpDir.mkdirs()
        def index = File.createTempFile('index', '.html', tmpDir)
        index.createNewFile()
        index.withWriter { c ->
            c.writeLine '<html>'
            c.writeLine '<body>'
            c.writeLine '<h1>Talks</h1>'
            c.writeLine '<dl>'
            subprojects.each { p ->
                def (date, title, location) = p.name.split('_')
                c.writeLine """<dt><a href="${p.name}/">${title.capitalize()}</a></dt>"""
                c.writeLine "<dd>${date} at ${location}</dd>"
            }
            c.writeLine '</dl>'
            c.writeLine '</body>'
            c.writeLine '</html>'
        }
        from(index) {
            rename(/.*/, 'index.html')
        }
    }
}

gradle.afterProject { project, projectState ->
    project.tasks.findAll { it.name == 'presentation' }.each {
        rootProject.publishGhPages.dependsOn it
    }
}

subprojects {
    repositories.jcenter()
    apply plugin: "org.asciidoctor.convert"
    apply plugin: "com.github.jruby-gradle.base"
    asciidoctorj {
        version = '1.5.4'
    }

    ext.backendPath = "${rootDir}/.backends/reveal.js"

    task presentation(type: AsciidoctorTask) {
        classpath = project.configurations.asciidoctor
        sourceDir = file('src/docs/asciidoc')
        sources {
            include 'presentation.adoc'
        }
        resources {
            from("${backendPath}/resources") {
                include '**/*'
            }
            from("${sourceDir}/../resources") {
                include '**/*'
            }
        }
        backends = ['html5']
        requires = ['slim', 'tilt', 'thread_safe']
        logDocuments = true
        options = [
                template_engine: 'slim',
                template_dirs  : ["${backendPath}/templates"]
        ]
    }
}
